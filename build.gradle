plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    // Publish libs to e.g maven-central
    id 'maven-publish'
    // sign artefacts before publishing to maven-central
    id 'signing'
    // publishing to Nexus that automates creating, closing, and releasing staging repositories
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
}

ext {
    jopenlibsVaultVersion = '6.2.0'
    slf4jVersion = '2.0.16'
    junitVersion = '5.11.0'
    assertJVersion = '3.26.3'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

compileJava {
    sourceCompatibility = 17
    targetCompatibility = 17
    // log details of usage of deprecated members or classes.
    options.deprecation = true
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation "io.github.jopenlibs:vault-java-driver:${jopenlibsVaultVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"

    // test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.assertj:assertj-core:${assertJVersion}"
    testImplementation "org.slf4j:slf4j-simple:${slf4jVersion}"
}

java {
    // create sources.jar as well
    withSourcesJar()
    // create javadoc.jar as well
    withJavadocJar()
}

jar {
    manifest.attributes provider: 'gradle'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'de.otto'
            artifactId = 'kafka-messaging-e2ee'
            description = 'Kafka Messaging End-To-End-Encryption'
            from components.java
            pom {
                name = 'Kafka Messaging E2EE'
                description = 'Kafka Messaging End-To-End-Encryption'
                url = 'https://github.com/otto-de/kafka-messaging-e2ee'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'andrekaplick5678'
                        name = 'Andre Kaplick'
                        organization = 'OTTO'
                        organizationUrl = 'https://www.github.com/otto-de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:otto-de/kafka-messaging-e2ee.git'
                    developerConnection = 'scm:git:ssh://github.com:otto-de/kafka-messaging-e2ee.git'
                    url = 'https://github.com/otto-de/kafka-messaging-e2ee/tree/main'
                }
            }
        }
    }
}

nexusPublishing {
    packageGroup = 'de.otto'
    repositories {
        sonatype()
    }
}

signing {
//    required {  false }
    sign publishing.publications.mavenJava
}
